// SCENES COLORS BASED ON SCENE EVENT IN HA
On ha_dn#evening=1 do
  NeopixelALL,255,0,0 // RED
Endon
On ha_dn#daytime=1 do
  NeopixelALL,255,0,0 // RED
Endon
On ha_scene#goodnight=1 do
  NeopixelALL,10,0,0 // RED
Endon
On ha_scene#kodi=1 do
  NeopixelALL,100,0,100 // PINK
Endon
On ha_scene#tv=1 do
  NeopixelALL,0,70,100 // LIGHT BLUE
Endon
On ha_scene#appletv=1 do
  NeopixelALL,100,100,0 // YELLOW
Endon
// END SCENES COLORS BASED ON SCENE EVENT IN HA

// MOTION ACTIONS AND COLORS OF SCENES
On ha_system#motion=1 do
  If [ha_scene#goodnight]=0
    NeopixelALL,1,0,0,255 // BLUE
  elseif [ha_scene#goodnight]=1
    NeopixelALL,1,0,0,80 // BLUE
  Endif
Endon

On ha_system#motion=0 do
  If [ha_scene#goodnight]=0
    If [ha_scene#appletv]=1
      NeopixelALL,1,100,100,0 // YELLOW
    elseif [ha_scene#tv]=1
      NeopixelALL,1,0,70,100 // LIGHT BLUE
    elseif [ha_scene#kodi]=1
      NeopixelALL,1,255,0,255 // PINK
    elseif [ha_dn#evening]=1
      NeopixelALL,1,255,0,0 // RED
    elseif [ha_dn#daytime]=1
      NeopixelALL,1,255,0,0 // RED
    Endif
  elseif [ha_scene#goodnight]=1
    NeopixelALL,1,20,0,0 // RED
  Endif
Endon
// END MOTION ACTIONS AND COLORS OF SCENES

// START BOOT ACTIONS
On System#Boot do
  TaskValueSet 11,1,0
  TaskValueSet 11,2,0
  TaskValueSet 11,3,0
  TaskValueSet 11,4,0
  GPIO,16,0
  NeopixelALL,50,0,0
  TimerSet,8,5
Endon

On Rules#Timer=8 do
  GPIO,16,1
  NeopixelALL,50,50,50
  TimerSet,7,2
Endon

On Rules#Timer=7 do
  TaskValueSet 3,3,0
Endon
// END START BOOT ACTIONS

// CALIBRATION TOUCH SENSOR
On CalibrateTouch do
  TaskValueSet 3,3,1
  GPIO,16,0
  TimerSet,8,3
  NeopixelALL,50,0,0
Endon
// END CALIBRATION TOUCH SENSOR

// RESET Wi-Fi CREDENTIALS
On resetwifi do
  WifiSSID
  WifiKey
  Reboot
  Save
Endon
// END RESET Wi-Fi CREDENTIALS
