// WHEN MOTION IS DETECTED
On ha_sys#mot=1 do
  If [ha_dn#gn]=0
    NeopixelALL,0,0,255
    GPIO 5,1 // FAN
    Publish homeassistant/other/fan_toilet,1
  Elseif [ha_dn#gn]=1 and [ha_sys#guest]=1
    NeopixelALL,0,0,80
    GPIO 5,1 // FAN
    Publish homeassistant/other/fan_toilet,1
  Elseif [ha_dn#gn]=1 and [ha_sys#guest]=0
    NeopixelALL,0,0,80
  Endif
Endon

On ha_sys#mot=0 do
  If [ha_dn#gn]=0
    If [ha_sc#atv]=1 
      NeopixelALL,100,100,0
    Elseif [ha_sc#tv]=1 
      NeopixelALL,0,70,100
    Elseif [ha_sc#kodi]=1 
      NeopixelALL,255,0,255
    Elseif [ha_dn#ev]=1 
      NeopixelALL,255,0,0
    Elseif [ha_dn#dt]=1 
      NeopixelALL,255,0,0
    Endif
    Gpio 5,0 // FAN
    Publish homeassistant/other/fan_toilet,0
  Elseif [ha_dn#gn]=1
    NeopixelALL,10,0,0
    GPIO 5,0 // FAN
    Publish homeassistant/other/fan_toilet,0
  Endif
Endon
// END

// SCENES COLORS BASED ON SCENE EVENT IN HA
On ha_dn#ev=1 do
  NeopixelALL,255,0,0 // RED
Endon
On ha_dn#dt=1 do
  NeopixelALL,255,0,0 // RED
Endon
On ha_dn#gn=1 do
  NeopixelALL,10,0,0 // RED
Endon
On ha_sc#kodi=1 do
  NeopixelALL,100,0,100 // PINK
Endon
On ha_sc#tv=1 do
  NeopixelALL,0,70,100 // LIGHT BLUE
Endon
On ha_sc#atv=1 do
  NeopixelALL,100,100,0 // YELLOW
Endon
// END

// Boot Initialization
On System#Boot do
  TaskValueSet 11,1,0
  TaskValueSet 11,2,0
  TaskValueSet 11,3,0
  TaskValueSet 11,4,0
  gpio,16,0
  NeopixelAll,50,0,0
  TimerSet,8,5
Endon

On Rules#Timer=8 do
  gpio,16,1
  NeopixelAll,50,50,50
Endon
// END

// Calibration Touch Sensor
On CalibrateTouch do
  TaskValueSet 3,3,1
  gpio,16,0
  TimerSet,8,3
  Neopixel,1,50,0,0
Endon
// END

// Reset Wi-Fi credentials
On resetwifi do
  WifiSSID
  WifiKey
  Reboot
  Save
Endon
// END
